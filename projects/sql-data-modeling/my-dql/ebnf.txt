query_clause =
    relation_clause
    [
        whitespace_list
        tuple_restriction_clause
    ]
    [
        whitespace_list
        attribute_restriction_clause
    ]

relation_clause =
    "get"
    whitespace_list
    "relations"
    whitespace_list
    relation_names_list

whitespace_list =
    {
        space
        |
        tab
        |
        newline
    }-

space =
    " "

tab =
    "	"

newline =
    "
"

relation_names_list =
    relation_name
    {
        whitespace_list
        ","
        whitespace_list
        relation_name
    }

tuple_restriction_clause =
    "then"
    whitespace_list
    "restrict"
    whitespace_list
    "tuples"
    whitespace_list
    "to"
    whitespace_list
    condition_conjunction_clauses_list

condition_conjunction_clauses_list =
    condition_disjunction_clauses_list
    {
        whitespace_list
        "and"
        whitespace_list
        condition_disjunction_clauses_list
    }

condition_disjunction_clauses_list =
    condition_negation_clause
    {
        whitespace_list
        "or"
        whitespace_list
        condition_negation_clause
    }

condition_negation_clause =
    [
        "not"
        whitespace_list
    ]
    condition_clause

condition_clause =
    qualified_attribute_name
    whitespace_list
    "equal"
    whitespace_list
    qualified_attribute_name

qualified_attribute_name =
    attribute_name
    whitespace_list
    "in"
    whitespace_list
    relation_name

attribute_name =
    '"'
    /[a-z_]+/
    '"'

relation_name =
    '"'
    /[a-z_]+/
    '"'

attribute_restriction_clause =
    "then"
    whitespace_list
    "restrict"
    whitespace_list
    "attributes"
    whitespace_list
    "to"
    whitespace_list
    qualified_attribute_names_list

qualified_attribute_names_list =
    qualified_attribute_name
    {
        whitespace_list
        ","
        whitespace_list
        qualified_attribute_name
    }
